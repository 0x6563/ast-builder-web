lexer: {{
    start: "json"

    json ->
        - when: /\s+/ tag: "space"
        - when: /-?(?:[0-9]|[1-9][0-9]+)(?:\.[0-9]+)?(?:[eE][-+]?[0-9]+)?\b/ tag: "number"
        - when: /"(?:\\["bfnrt\/\\]|\\u[a-fA-F0-9]{4}|[^"\\])*"/ 
            tag: "string"
            highlight: "string"
        - when: "{" tag: "{"
        - when: "}" tag: "}"
        - when: "[" tag: "["
        - when: "]" tag: "]"
        - when: "," tag: ","
        - when: ":" tag: ":"
        - when: "true" tag: "true"
        - when: "false" tag: "false"
        - when: "null" tag: "null"
}}

grammar: {{
    // Uncomment the line below to allow experimental diagram generation
	// postprocessorOverride: ${ _ =>_ }

    json -> _ (object | array) _ 

    object -> "{" _ "}" 
        | "{" _ pair (_ "," _ pair)* _ "}" 

    array -> "[" _ "]" 
        | "[" _ value (_ "," _ value)* _ "]"  

    value ->
        object
        | array
        | number
        | string
        | "true"
        | "false"
        | "null"

    number -> $number

    string -> $string

    pair -> key:k _ ":" _ value:v

    key -> string

    _ -> $space?
}}
